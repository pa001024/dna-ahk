删MOD取近战属性
=LAMBDA(属性,i,IFERROR(取武器属性(近战MOD,i,0,属性,"近战武器"),取武器属性(近战MOD,0,0,属性,"近战武器")))
删MOD取远程属性
=LAMBDA(属性,i,IFERROR(取武器属性(远程MOD,i,0,属性,"远程武器"),取武器属性(远程MOD,0,0,属性,"远程武器")))
删MOD取同律属性
=LAMBDA(属性,i,IFERROR(取武器属性(同律MOD,i,0,属性,$A$123),取武器属性(同律MOD,0,0,属性,$A$123)))
删BUFF取近战属性
=LAMBDA(属性,i,IFERROR(取武器属性(近战MOD,0,i,属性,"近战武器"),取武器属性(近战MOD,0,0,属性,"近战武器")))
删BUFF取远程属性
=LAMBDA(属性,i,IFERROR(取武器属性(远程MOD,0,i,属性,"远程武器"),取武器属性(远程MOD,0,0,属性,"远程武器")))
删BUFF取同律属性
=LAMBDA(属性,i,IFERROR(取武器属性(同律MOD,0,i,属性,$A$123),取武器属性(同律MOD,0,0,属性,$A$123)))
取武器属性
=LAMBDA(MOD,删MOD,删BUFF,属性,类型,IF(包含(属性,{"独立增伤","无视防御"}),取武器乘法属性(MOD,删MOD,删BUFF,属性,类型),取武器加法属性(MOD,删MOD,删BUFF,属性,类型)))
取武器加法属性
=LAMBDA(MOD,删MOD,删BUFF,属性,类型,
    基础(LEFT(类型,2)&属性) * SUM(
    IF(包含(属性,{"攻击","暴击","暴伤","触发","攻速","多重"}),1,0),
    数组删除(IFERROR(
        MAP((OFFSET(MOD,0,5)+1)/IF(OFFSET(MOD,0,2)="金",11,IF(OFFSET(MOD,0,2)="绿",4,6)),LAMBDA(a, IF(a>1,1,a)))*
        MAP(MOD,LAMBDA(M,INDEX(武器MOD表,MATCH(1,(武器MOD表[名称]=M)*(武器MOD表[类型]=类型),0),MATCH(属性,武器MOD表[#标题],0)))),0),删MOD),
    IF(属性="攻击",INDIRECT(LEFT(类型,2)&"精通"),取加法角色属性(属性,0,删BUFF)),
    IF(属性="攻击",数组删除(取BUFF属性("武器"&属性),删BUFF),0),
    IF(属性="增伤",数组删除(取BUFF属性("武器伤害"),删BUFF),0),
    数组删除(取BUFF属性(IF(ISNUMBER(FIND("近战",类型)),"近战","远程")&属性),删BUFF)+0)
)
取武器乘法属性
=LAMBDA(MOD,删MOD,删BUFF,属性,类型,
    PRODUCT(取乘法角色属性(属性,0,删BUFF)+1,
        数组删除(IFERROR(
        MAP((OFFSET(MOD,0,5)+1)/IF(OFFSET(MOD,0,2)="金",11,IF(OFFSET(MOD,0,2)="绿",4,6)),LAMBDA(a, IF(a>1,1,a)))*
        MAP(MOD,LAMBDA(M,INDEX(武器MOD表,MATCH(1,(武器MOD表[名称]=M)*(武器MOD表[类型]=类型),0),MATCH(属性,武器MOD表[#标题],0)))),0),删MOD)+1,
    数组删除(取BUFF属性(IF(ISNUMBER(FIND("近战",类型)),"近战","远程")&属性),删BUFF)+1)-1
)
防御乘区
=LAMBDA(无视防御1,1-(1-(1-防御系数/(30+防御系数-MAX(0,MIN(20,MIN(80,敌方等级)-角色等级)))))*(1-无视防御1))
昂扬背水乘区
=LAMBDA(a,b,(1+a*当前生命百分比)*(1+4*b*(1-MAX(0.25,当前生命百分比))*(1.5-MAX(0.25,当前生命百分比))))
触发乘区
=LAMBDA(伤害类型,武器攻击,总攻击,触发率,IF(IF(伤害类型="贯穿","生命",IF(伤害类型="切割","护盾","战姿"))=B$4,触发率*((1+IF(伤害类型="贯穿",0.5,IF(伤害类型="切割",1,IF(伤害类型="震荡",1,0)))*武器攻击/总攻击)-1)+1,1))
删MOD取角色属性
=LAMBDA(属性,i,IFERROR(IF(包含(属性,{"独立增伤","无视防御"}),取乘法角色属性(属性,i,0),取加法角色属性(属性,i,0)),取角色总属性(属性)))
删BUFF取角色属性
=LAMBDA(属性,i,IFERROR(IF(包含(属性,{"独立增伤","无视防御"}),取乘法角色属性(属性,0,i),取加法角色属性(属性,0,i)),取角色总属性(属性)))
取角色总属性
=LAMBDA(属性,IF(包含(属性,{"独立增伤","无视防御"}),取乘法角色属性(属性,0,0),取加法角色属性(属性,0,0)))
取加法角色属性
=LAMBDA(属性,删MOD,删BUFF,
    上下限(属性, 基础(属性) * SUM(
        IF(包含(属性,{"攻击","生命","护盾","防御","神智","威力","耐久","效能","范围"}),1,0),
        IF(包含(属性,{"攻击","生命","护盾","防御"}),和鸣增益,0),
        取近战属性(属性),
        取远程属性(属性),
        取角色属性(属性),
        数组删除(取角色MOD属性(属性,SUM(数组删除(取BUFF属性("MOD属性"),删BUFF)+0)),删MOD),
        数组删除(取BUFF属性(属性),删BUFF)+0
    ) + SUM(数组删除(取角色MOD属性("固定"&属性,SUM(数组删除(取BUFF属性("MOD属性"),删BUFF)+0)),删MOD),数组删除(取BUFF属性("固定"&属性),删BUFF)+0)
))
取乘法角色属性
=LAMBDA(属性,删MOD,删BUFF,
    上下限(属性, (基础(属性)+1) * PRODUCT(
        IF((属性="独立增伤")*(中枢MOD="磐石"),1-0.15,1),
        取近战属性(属性)+1,
        取远程属性(属性)+1,
        取角色属性(属性)+1,
        数组删除(取角色MOD属性(属性,SUM(数组删除(取BUFF属性("MOD属性"),删BUFF)+0)),删MOD)+1,
        取BUFF属性(属性)+1
    )-1
))
基础
=LAMBDA(键,IFERROR(INDIRECT("基础"&键),1))
取上限
=LAMBDA(键,IFERROR(INDIRECT(键&"上限"),99999999))
取下限
=LAMBDA(键,IFERROR(INDIRECT(键&"下限"),-99999999))
上下限
=LAMBDA(键,值,MAX(MIN(值,取上限(键)),取下限(键)))
包含
=LAMBDA(值,数组,ISNUMBER(MATCH(值,数组,0)))
表取值
=LAMBDA(键,表,表头,列,IFERROR(VLOOKUP(键,表,MATCH(列,表头,0),0),0))
数组删除
=LAMBDA(数组,位置,FILTER(数组,SEQUENCE(ROWS(数组))<>位置))
取角色属性
=LAMBDA(属性,表取值(角色,角色表,角色表[#标题],属性))
取近战属性
=LAMBDA(属性,(5+$D$59)/10*表取值(近战武器,近战武器表,近战武器表[#标题],属性))
取远程属性
=LAMBDA(属性,(5+$D$91)/10*表取值(远程武器,远程武器表,远程武器表[#标题],属性))
取BUFF属性
=LAMBDA(属性,IFERROR(BUFF层数乘数*表取值(BUFF,BUFF表,BUFF表[#标题]属性)))
角色MOD等级乘数
=MAP(($J$7:$J$14+1)/IF($G$7:$G$14="金",11,IF($G$7:$G$14="绿",4,6)),LAMBDA(a,IF(a>1,1,a)))
BUFF层数乘数
=IFERROR(MAP(
    表取值(BUFF,BUFF表,BUFF表[#标题],"a"),
    表取值(BUFF,BUFF表,BUFF表[#标题],"b"),
    OFFSET(BUFF,0,1),
    表取值(BUFF,BUFF表,BUFF表[#标题],"dx"),
    表取值(BUFF,BUFF表,BUFF表[#标题],"lx"),
    LAMBDA(a,b,x,dx,lx,
        1/a*(1+1/b*(IF(x="",dx,x)-lx)))
    )
,1)
取角色MOD属性
=LAMBDA(属性,加成,IFERROR(
        角色MOD等级乘数*
        MAP(角色MOD,LAMBDA(MOD,IF(包含(表取值(MOD,角色MOD表,角色MOD表[#标题],"系列"),{"百首","狮鹫","契约者"}),1+加成,1)))*
        VLOOKUP(角色MOD,角色MOD表,MATCH(属性,角色MOD表[#标题],0),0),0))
取单角色MOD属性
=LAMBDA(MOD,属性,IFERROR(
        MAP((OFFSET(MOD,0,5)+1)/IF(OFFSET(MOD,0,2)="金",11,IF(OFFSET(MOD,0,2)="绿",4,6)),LAMBDA(a,IF(a>1,1,a)))*
        IF(包含(表取值(MOD,角色MOD表,角色MOD表[#标题],"系列"),{"百首","狮鹫","契约者"}),1+SUM(取BUFF属性("MOD属性")+0),1)*
        VLOOKUP(MOD,角色MOD表,MATCH(属性,角色MOD表[#标题],0),0),0))